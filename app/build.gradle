plugins {
    id 'com.android.application'
    id 'kotlin-android'
//    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
}

static def releaseTime() {
    return new Date().format("yyyyMMdd_HHmm", TimeZone.getTimeZone("GMT+8"))
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.gucheng.statistic.helper"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 8
        versionName "1.3.6"

        flavorDimensions "versionCode"

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'arm64-v8a', 'x86_64' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def propsFile = file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
//    kotlinOptions {
//        jvmTarget = '1.8'
//    }

    productFlavors {
        tencent {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"tencent"]
        }
        huawei {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"huawei"]
        }
    }
    namespace 'com.gucheng.statistichelper'

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName ="statishelper_${variant.productFlavors[0].name}_v${defaultConfig.versionName}_${releaseTime()}.apk"
            }
    }

}

dependencies {
    implementation libs.kotlin.stdlib
    implementation libs.kotlin.stdlib.jdk7
    api libs.kotlinx.coroutines.core
    api libs.kotlinx.coroutines.android
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.core.ktx
    implementation project(path: ':checkversion')
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.biometric
    implementation libs.androidx.legacy.support.v4
    implementation libs.mpandroidchart
    implementation libs.androidx.fragment.ktx
    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core
    implementation libs.umeng.common
    implementation libs.umeng.asms
    implementation libs.androidx.room.runtime
    ksp libs.androidx.room.compiler
    implementation libs.androidx.room.ktx
    implementation project(':x')
    implementation libs.rxkotlin
    implementation libs.rxandroid
    testImplementation libs.androidx.room.testing
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.common.java8
}